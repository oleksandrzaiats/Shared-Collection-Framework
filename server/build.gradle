buildscript {
    repositories {
        jcenter()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'spring-boot'

jar {
    baseName = 'shared-collection-framework'
    version = '0.0.1-SNAPSHOT'
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.0'


configurations {
    jettyRun
}

task jettyRunWar(type: JavaExec, dependsOn: war, group: 'jetty') {
    main = 'org.eclipse.jetty.runner.Runner'
    args = ['--path', '/', war.archivePath]
    classpath configurations.jettyRun
}

repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

dependencies {
    compile project(':shared')

    // Spring
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework:spring-core:$springVersion")
    compile("org.springframework:spring-webmvc:$springVersion")
    compile("org.springframework:spring-web:$springVersion")
    compile("org.springframework:spring-tx:$springVersion")
    compile("org.springframework:spring-orm:$springVersion")
    compile("org.springframework:spring-test:$springVersion")
    compile("org.springframework.data:spring-data-commons-core:1.4.1.RELEASE")
    compile("org.springframework.boot:spring-boot-autoconfigure:1.3.3.RELEASE")
    compile("org.thymeleaf:thymeleaf-spring4")
    compile("nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect")
    compile("commons-fileupload:commons-fileupload:1.3.1")

    // Spring Security
    compile("org.springframework.security:spring-security-core:$springSecurityVersion")
    compile("org.springframework.security:spring-security-config:$springSecurityVersion")
    compile("org.springframework.security:spring-security-web:$springSecurityVersion")
    compile("io.jsonwebtoken:jjwt:0.5.1")

    // Database
    compile("org.hibernate:hibernate-validator:5.2.4.Final")
    compile("org.hibernate:hibernate-core:$hibernateVersion")
    compile("org.hibernate:hibernate-entitymanager:$hibernateVersion")
    compile("mysql:mysql-connector-java:5.1.38")

    // Jetty
    jettyRun "org.eclipse.jetty:jetty-runner:$jettyVersion"

    // Test
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.0.1.RELEASE'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn test
    sourceDir 'src/main/asciidoc'
}



